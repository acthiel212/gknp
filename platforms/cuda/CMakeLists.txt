#---------------------------------------------------
# OpenMM GKNP Plugin Cuda Platform
#----------------------------------------------------

# Collect up information about the version of the OpenMM library we're building
# and make it available to the code so it can be built into the binaries.

SET(OPENMMGKNPCUDA_LIBRARY_NAME GKNPPluginCuda)

SET(SHARED_TARGET ${OPENMMGKNPCUDA_LIBRARY_NAME})


# These are all the places to search for header files which are
# to be part of the API.
SET(API_INCLUDE_DIRS "${CMAKE_CURRENT_SOURCE_DIR}/include" "${CMAKE_CURRENT_SOURCE_DIR}/include/internal")

# Locate header files.
SET(API_INCLUDE_FILES)
FOREACH(dir ${API_INCLUDE_DIRS})
    FILE(GLOB fullpaths ${dir}/*.h)
    SET(API_INCLUDE_FILES ${API_INCLUDE_FILES} ${fullpaths})
ENDFOREACH(dir)

# collect up source files
SET(SOURCE_FILES) # empty
SET(SOURCE_INCLUDE_FILES)

FILE(GLOB_RECURSE src_files  ${CMAKE_CURRENT_SOURCE_DIR}/src/*.cpp ${CMAKE_CURRENT_SOURCE_DIR}/${subdir}/src/*.c)
FILE(GLOB incl_files ${CMAKE_CURRENT_SOURCE_DIR}/src/*.h)
SET(SOURCE_FILES         ${SOURCE_FILES}         ${src_files})   #append
SET(SOURCE_INCLUDE_FILES ${SOURCE_INCLUDE_FILES} ${incl_files})
INCLUDE_DIRECTORIES(BEFORE ${CMAKE_CURRENT_SOURCE_DIR}/include)

INCLUDE_DIRECTORIES(BEFORE ${CMAKE_CURRENT_SOURCE_DIR}/src)
INCLUDE_DIRECTORIES(BEFORE ${CMAKE_SOURCE_DIR}/platforms/cuda/include)
INCLUDE_DIRECTORIES(BEFORE ${CMAKE_SOURCE_DIR}/platforms/cuda/src)
INCLUDE_DIRECTORIES(BEFORE ${CMAKE_BINARY_DIR}/platforms/cuda/src)
INCLUDE_DIRECTORIES(BEFORE ${CMAKE_SOURCE_DIR}/gaussvol)

# Set variables needed for encoding kernel sources into a C++ class

SET(CU_SOURCE_DIR ${CMAKE_CURRENT_SOURCE_DIR}/src)
SET(CU_SOURCE_CLASS CudaGKNPKernelSources)
SET(CU_KERNELS_CPP ${CMAKE_CURRENT_BINARY_DIR}/src/${CU_SOURCE_CLASS}.cpp)
SET(CU_KERNELS_H ${CMAKE_CURRENT_BINARY_DIR}/src/${CU_SOURCE_CLASS}.h)
SET(SOURCE_FILES ${SOURCE_FILES} ${CU_KERNELS_CPP} ${CU_KERNELS_H})
INCLUDE_DIRECTORIES(BEFORE ${CMAKE_CURRENT_BINARY_DIR}/src)

# Create the library

INCLUDE_DIRECTORIES(${CUDA_INCLUDE_DIR})

FILE(GLOB CUDA_KERNELS ${CU_SOURCE_DIR}/kernels/*.cu)
ADD_CUSTOM_COMMAND(OUTPUT ${CU_KERNELS_CPP} ${CU_KERNELS_H}
    COMMAND ${CMAKE_COMMAND}
    ARGS -D CU_SOURCE_DIR=${CU_SOURCE_DIR} -D CU_KERNELS_CPP=${CU_KERNELS_CPP} -D CU_KERNELS_H=${CU_KERNELS_H} -D CU_SOURCE_CLASS=${CU_SOURCE_CLASS} -P ${CMAKE_SOURCE_DIR}/platforms/cuda/EncodeCLFiles.cmake
    DEPENDS ${CUDA_KERNELS}
)
SET_SOURCE_FILES_PROPERTIES(${CU_KERNELS_CPP} ${CU_KERNELS_H} PROPERTIES GENERATED TRUE)
ADD_LIBRARY(${SHARED_TARGET} SHARED ${SOURCE_FILES} ${SOURCE_INCLUDE_FILES} ${API_INCLUDE_FILES})

TARGET_LINK_LIBRARIES(${SHARED_TARGET} ${CUDA_LIBRARIES})
TARGET_LINK_LIBRARIES(${SHARED_TARGET} OpenMM)
TARGET_LINK_LIBRARIES(${SHARED_TARGET} OpenMMCuda)
TARGET_LINK_LIBRARIES(${SHARED_TARGET} ${SHARED_GKNP_TARGET})
TARGET_LINK_LIBRARIES(${SHARED_TARGET} ${GAUSSVOLLIB_NAME})

SET_TARGET_PROPERTIES(${SHARED_TARGET} PROPERTIES
    COMPILE_FLAGS "-DOPENMM_BUILDING_SHARED_LIBRARY ${EXTRA_COMPILE_FLAGS}"
    LINK_FLAGS "${EXTRA_COMPILE_FLAGS}")

INSTALL(TARGETS ${SHARED_TARGET} DESTINATION ${CMAKE_INSTALL_PREFIX}/lib/plugins)
# Ensure that links to the main Cuda library will be resolved.
IF (APPLE)
    SET(CUDA_LIBRARY libOpenMMCuda.dylib)
    INSTALL(CODE "EXECUTE_PROCESS(COMMAND install_name_tool -change ${CUDA_LIBRARY} @loader_path/${CUDA_LIBRARY} ${CMAKE_INSTALL_PREFIX}/lib/plugins/lib${SHARED_TARGET}.dylib)")
ENDIF (APPLE)

SUBDIRS (tests)

